(* gramatica.ebnf - Definição da gramática da linguagem AfetoLang *)

(* Bloco de declarações delimitado por chaves *)
bloco            = "{" { declaracao } "}" ;

(* Tipos de declaração possíveis *)
declaracao       = atribuicao
                 | condicional
                 | repeticao
                 | entrada
                 ;

(* Atribuição de valor a uma variável *)
atribuicao       = "guarde" identificador "como" comparacao ";" ;

(* Estrutura condicional *)
condicional      = "se" "(" comparacao ")" "entao" bloco [ "senão" bloco ] ;

(* Estrutura de repetição *)
repeticao        = "enquanto" "(" comparacao ")" bloco ;

(* Entrada (mostre) ou leitura isolada (escute) *)
entrada    = "mostre" "(" comparacao ")" ";" ;

(* Expressões aritméticas e booleanas simples *)
comparacao       = expressao { operador_comparacao expressao } ;
expressao        = termo { operador_soma termo } ;
termo            = fator { operador_mult fator } ;
fator            = identificador
                 | numero
                 | texto
                 | "+" fator
                 | "-" fator
                 | "escute" "(" ")"
                 | "(" comparacao ")" ;

(* Operadores *)
operador_comparacao = ">" | "<" | "==" | "!=" | ">=" | "<=" ;
operador_soma    = "+" | "-" ;
operador_mult    = "*" | "/" ;

(* Elementos léxicos *)
identificador    = letra { letra | "_" | digito } ;
numero           = digito { digito } ;
texto            = '"' { caractere_sem_aspas } '"' ;

(* Conjuntos auxiliares *)
letra            = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
                 | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t"
                 | "u" | "v" | "w" | "x" | "y" | "z"
                 | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
                 | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
                 | "U" | "V" | "W" | "X" | "Y" | "Z" ;

digito           = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

caractere_sem_aspas = ? qualquer caractere exceto aspas ? ;