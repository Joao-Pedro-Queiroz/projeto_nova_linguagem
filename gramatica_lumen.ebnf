BlocoPrincipal     = "inicio", { Instrucao }, "fim" ;

Instrucao          = ";"
                   | identificador "Ã©" ExpressaoOu ";"
                   | "exibir" "(" ExpressaoOu ")" ";"
                   | "guardar" identificador "como" tipo [ "com" ExpressaoOu ] ";"
                   | "quando" "(" ExpressaoOu ")" BlocoPrincipal [ "senao" BlocoPrincipal ]
                   | "enquanto" "(" ExpressaoOu ")" BlocoPrincipal ;

ExpressaoOu        = ExpressaoE { "ou" ExpressaoE } ;

ExpressaoE         = ExpressaoRel { "e" ExpressaoRel } ;

ExpressaoRel       = Expressao [ OperadorRelacional Expressao ] ;

Expressao          = Termo { ("mais" | "menos" | "concatena") Termo } ;

Termo              = Fator { ("vezes" | "dividido") Fator } ;

Fator              = numero
                   | texto
                   | booleano
                   | identificador
                   | "mais" Fator
                   | "menos" Fator
                   | "nao" Fator
                   | "(" ExpressaoOu ")"
                   | "perguntar" "(" ")" ;

OperadorRelacional = "igual" | "maior" | "menor" ;

tipo               = "numero" | "booleano" | "texto" ;

identificador      = letra { letra | digito | "_" } ;
numero             = digito { digito } ;
texto              = '"' { qualquer_caractere_que_nao_seja_aspas } '"' ;
booleano           = "verdadeiro" | "falso" ;