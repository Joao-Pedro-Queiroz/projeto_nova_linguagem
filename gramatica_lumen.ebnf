BlocoPrincipal     = "INICIO", { Instrucao }, "FIM" ;

Instrucao          = ";"
                   | identificador "Ã‰" ExpressaoOu ";"
                   | "EXIBIR" "(" ExpressaoOu ")" ";"
                   | "GUARDAR" identificador "COMO" tipo [ "COM" ExpressaoOu ] ";"
                   | "QUANDO" "(" ExpressaoOu ")" BlocoPrincipal [ "SENAO" BlocoPrincipal ]
                   | "ENQUANTO" "(" ExpressaoOu ")" BlocoPrincipal ;

ExpressaoOu        = ExpressaoE { "OU" ExpressaoE } ;

ExpressaoE         = ExpressaoRel { "E" ExpressaoRel } ;

ExpressaoRel       = Expressao { ("IGUAL" | "MAIOR" | "MENOR") Expressao } ;

Expressao          = Termo { ("MAIS" | "MENOS" | "CONCATENA") Termo } ;

Termo              = Fator { ("VEZES" | "DIVIDIDO") Fator } ;

Fator              = numero
                   | texto
                   | booleano
                   | identificador
                   | "MAIS" Fator
                   | "MENOS" Fator
                   | "NAO" Fator
                   | "(" ExpressaoOu ")"
                   | "PERGUNTAR" "(" ")" ;

tipo               = "NUMERO" | "BOOLEANO" | "TEXTO" ;

identificador      = letra { letra | digito | "_" } ;
numero             = digito { digito } ;
texto              = '"' { qualquer_caractere_que_nao_seja_aspas } '"' ;
booleano           = "VERDADEIRO" | "FALSO" ;

letra             = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
                    | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
                    | "U" | "V" | "W" | "X" | "Y" | "Z" ;

digito              = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

qualquer_caractere_que_nao_seja_aspas = ? qualquer caractere exceto aspas ? ;