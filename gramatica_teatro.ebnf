ato             = "ato" identificador "com" "duracao" numero bloco ;

bloco           =  "{" { declaracao } "}" ;

declaracao      = atribuicao
                | condicional
                | repeticao
                | fala
                | entrada ;

(* Atribuição de valor a um personagem (variável) *)
atribuicao      = "diretor" identificador "como" comparacao ";" ;

(* Estrutura de condição e repetição *)
condicional     = "se" "(" comparacao ")" "entao" cena [ "senao" cena ] ;

repeticao       = "enquanto" "(" comparacao ")" cena ;

cena            = "cena" bloco ;

(* Fala exibida na peça *)
fala            = "fale" "(" comparacao ")" ";" ;

(* Entrada improvisada *)
entrada         = "ator" identificador "como" "improviso" "(" ")" ";" ;

(* Expressões *)
comparacao      = expressao { operador_comparacao expressao } ;
expressao       = termo { operador_soma termo } ;
termo           = fator { operador_mult fator } ;
fator           = identificador
                | numero
                | texto
                | "+" fator
                | "-" fator
                | "(" comparacao ")" ;

(* Operadores *)
operador_comparacao = ">" | "<" | "==" | "!=" | ">=" | "<=" ;
operador_soma       = "+" | "-" ;
operador_mult       = "*" | "/" ;

(* Elementos léxicos *)
identificador    = letra { letra | "_" | digito } ;
numero           = digito { digito } ;
texto            = '"' { caractere_sem_aspas } '"' ;

(* Conjuntos auxiliares *)
letra             = "a" | "b" | ... | "z" | "A" | "B" | ... | "Z" ;
digito            = "0" | "1" | ... | "9" ;
caractere_sem_aspas = ? qualquer caractere exceto aspas ? ;
